

/*

--
-- Table structure for table `event_poringrace_rank`
--

CREATE TABLE IF NOT EXISTS `event_poringrace_rank` (
	`id` INT(11) UNSIGNED NOT NULL AUTO_INCREMENT,
	`aid` INT(11) UNSIGNED NOT NULL DEFAULT '0',
	`cid` INT(11) UNSIGNED NOT NULL DEFAULT '0',
	`name` VARCHAR(30) NOT NULL DEFAULT '',
	`record` INT(11) UNSIGNED NOT NULL DEFAULT '0',
	`time` DATETIME NOT NULL DEFAULT '0000-00-00 00:00:00',
	PRIMARY KEY (`id`)
) ENGINE=MyISAM; 

*/



prontera,155,171,5	script	Race Ranking::poring_rc_rank	875,{
	doevent "poring_rc_main::OnRank";
}

//prontera,155,172,5	duplicate(poring_rc_rank)	Race Ranking#1	875
// prontera,155,173,5	duplicate(poring_rc_rank)	Race Ranking#2	875
// prontera,155,174,5	duplicate(poring_rc_rank)	Race Ranking#3	875
// prontera,155,175,5	duplicate(poring_rc_rank)	Race Ranking#4	875
// prontera,155,176,5	duplicate(poring_rc_rank)	Race Ranking#5	875
// prontera,155,177,5	duplicate(poring_rc_rank)	Race Ranking#6	875
// prontera,155,178,5	duplicate(poring_rc_rank)	Race Ranking#7	875


prontera,155,181,5	script	poring_rc_portal	45,2,2,{
	OnTouch:
		doevent "poring_rc_main::OnEnter";
		end;
}

poring_rc,68,111,4	script	Poring Race Track	875,{
	emotion e_go;
	end;
}

-	script	poring_rc_main	-1,{
	end;

OnWhisperGlobal:
	if ( getgmlevel() >= 90 ) 
	{
		if (@whispervar0$ == "activar")
			goto OnEventStart;
	}
end;

	
	OnFinishRound:
		if ( .status == 2 ) {
			@checkpoint = 0;
			@round_count++;
			if ( @round_count >= .round ) {
				.total_winner_count++;
				switch ( .total_winner_count ) {
					case 1: 
						getitem 501,1;
						#CASHPOINTS += 20;
						break;
					case 2:
						getitem 502,1;
						#CASHPOINTS += 10;
						break;
					case 3:
						getitem 503,1;
						#CASHPOINTS += 5;
						break;
					default:
						getitem 501,1;
						break;
				}
				.@name$ = strcharinfo(0);
				.@second = getnpctimer(0);
				query_sql( "INSERT INTO `event_poringrace_rank` ( `aid`,`cid`,`name`,`record`,`time` ) VALUES ( "+getcharid(3)+","+getcharid(0)+",'"+escape_sql( .@name$ )+"',"+.@second+",NOW() ) " );
				mes "Eres el"+F_GetNumSuffix( .total_winner_count )+"Jugadores que terminaron las pistas.";
				mes "^FF0000( veces: "+sprintf( "%d.%03d",( .@second / 1000 ),( .@second % 1000 ) )+" segundos )^000000";
				mapannounce .npc_map$,.@name$+" - "+F_GetNumSuffix( .total_winner_count )+"los jugadores terminaron las pistas. (tiempos:"+sprintf( "%d.%03d",( .@second / 1000 ),( .@second % 1000 ) )+" segundos )",bc_map;
				// query records
				close2;
				donpcevent .npc_name$+"::OnRankUpdate";
				warp "SavePoint",0,0;
			}
			else {
				announce "[Pista de carreras]: Ronda"+@round_count+" / "+.round+"!!",bc_self;
			}
		}
		end;
		
	OnRank:
		mes "[Pista de carreras]";
		if ( !.cid_size ) {
			mes "No se encontraron registros.";
		}
		else {
			.@cid = getcharid(0);
			for ( .@i = 0; .@i < .cid_size; .@i++ ) {
				mes F_GetNumSuffix( .@i+1 )+" - "+.name$[.@i];
				mes "Record -^777777 "+sprintf( "%d.%03d",( .record[.@i] / 1000 ), ( .record[.@i] % 1000 ) )+" segundo^000000";
				mes " ";
				if ( .cid[.@i] == .@cid ) 
					.@buff++;
			}
			if ( .@buff ) 
				if ( select( "Buff Me","Cancel" ) == 1 ) {
					specialeffect2 EF_HEAL2; percentheal 100,100;
					specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
					specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
				}
		}
		close;
		
	OnInit:
		// how many round
		.round = 5;
		
		// event rules / broadcast message
		setarray .message$,
			"Este es el evento de carreras.",
			"Todos los jugadores requieren usar MOUNT.",
			"Se sancionará a los jugadores que infrinjan las reglas.",
			"Existe algún punto de penalti aleatorio, ten cuidado.",
			"Hay varios puntos de control que debe pasar antes de poder terminar una ronda.",
			"Por cierto, es una carrera en sentido antihorario.",
			"¡Disfruta y corre!";
		.message_size = getarraysize( .message$ );
		
		.npc_name$ = strnpcinfo(3);
		getmapxy( .npc_map$,.npc_x,.npc_y,1,"Poring Race Track" );
		setmapflag .npc_map$,mf_noskill;
		setmapflag .npc_map$,mf_noteleport;
		setmapflag .npc_map$,mf_nomemo;
		setmapflag .npc_map$,mf_nosave;
		sleep 1000;
		disablenpc "poring_rc_portal";
		mapwarp .npc_map$,.npc_map$,.npc_x,.npc_y;
		movenpc "poring_rc_checkpoint#0",.npc_x,.npc_y;
	
	OnRankUpdate:
		deletearray .cid;
		query_sql( "SELECT `cid`,`name`,`record` FROM `event_poringrace_rank` ORDER BY `record` LIMIT 10",.cid,.name$,.record );
		.cid_size = getarraysize( .cid );
		end;
		
	// OnClock0000:
	// OnClock0600:
	// OnClock1200:
	// OnClock1800:
	OnMinute00:
	OnEventStart:
		if ( !.status ) {
			.status = 1;
			.total_winner_count = 0;
			
			for ( .@i = 0; .@i <= 6; .@i++ ) {
				donpcevent "#poring_rc_wall_1"+.@i+"::OnEnable";
				donpcevent "#poring_rc_wall_2"+.@i+"::OnEnable";
			}
			enablenpc "poring_rc_portal";
			for ( .@i = 3; .@i > 0; .@i-- ) {
				announce "[Pista de carreras]: El evento de carreras comenzará en"+.@i+"minutos. Únase al Portal.",bc_all;
				sleep 60000;
			}
			disablenpc "poring_rc_portal";
			announce "[Pista de carreras]: El evento de carreras ha comenzado.",bc_all;
			
			for ( .@i = 0; .@i < .message_size; .@i++ ) {
				mapannounce .npc_map$,.message$[.@i],bc_map;
				npctalk .message$[.@i];
				sleep 5000;
			}
			
			for ( .@i = 3; .@i > 0; .@i-- ) {
				mapannounce .npc_map$,"[Pista de carreras]: "+.@i,bc_map;
				npctalk ""+.@i;
				sleep 1000;
			}
			
			mapannounce .npc_map$,"[Pista de carreras]: GO!!!",bc_map;
			npctalk "GO!!";
			emotion e_go;
			
			for ( .@i = 0; .@i <= 6; .@i++ ) {
				donpcevent "#poring_rc_wall_1"+.@i+"::OnDisable";
				donpcevent "#poring_rc_wall_2"+.@i+"::OnDisable";
			}
			
			.status = 2;
			initnpctimer;
		}
		end;
		
	OnTimer2700000: // 45 minutes
		.status = 0;
		stopnpctimer;
		mapannounce .npc_map$,"[Pista de carreras]: ¡Se acabó el tiempo del evento de carrera!",bc_map;
		sleep 5000;
		mapannounce .npc_map$,"[Pista de carreras]: Resultado del evento de carrera: obtuvimos"+.total_winner_count+"ganadores.",bc_map;
		sleep 10000;
		mapwarp .npc_map$,.npc_map$,.npc_x,.npc_y;
		end;
		
	OnEnter:
		if ( .status == 1 ) {
			if ( ismounting() ) {
				dispbottom "[Pista de carreras]: Quita tu montura.";
			}
			else if ( checkriding() ) {
				dispbottom "[Pista de carreras]: Quita tu montura.";
			}
			else {
				@checkpoint = 0;
				@round_count = 0;
				warp .npc_map$,.npc_x,.npc_y;
			}
		}
		end;
}


poring_rc,1,1,0	script	poring_rc_checkpoint#0	139,4,4,{
	OnTouch:
		.@checkpoint = atoi( strnpcinfo(2) );
		
		if ( getvariableofnpc( .status,"poring_rc_main" ) == 2 ) {
		
			if ( @checkpoint >= .@checkpoint ) {
				dispbottom "Ha llegado a este punto de control #"+( ( .@checkpoint != 0 )? .@checkpoint:( @round_count == 0 ) ? 0:.max_checkpoint )+"previamente.";
			}
			else if ( @checkpoint != ( .@checkpoint - 1 ) ) {
				mes "No llegaste al punto de control #"+( ( .@checkpoint == 0 )? .max_checkpoint:( .@checkpoint-1 ) )+".";
				close2;
				// warp 
			}
			else {
				.@next = (( .@checkpoint+1 ) % .max_checkpoint );
				@checkpoint = .@next - 1;
				
				
				getmapxy( .@map$,.@x,.@y,1,"poring_rc_checkpoint#"+.@next );
				viewpoint 1,.@x,.@y,1,0x00EE00;
				announce "Alcanzaste"+F_GetNumSuffix( .@checkpoint )+"Punto de control"+F_GetNumSuffix( .@next == 0 ? .max_checkpoint:.@next )+"Punto de control en ("+.@x+","+.@y+" )",bc_self|bc_blue;
				// announce "You reached "+F_GetNumSuffix( .@checkpoint )+" Checkpoint. ",bc_self;
				
				if ( .@next == 1 ) {
					doevent "poring_rc_main::OnFinishRound";
				}
			}
		}
		end;
		
	OnInit:
		.max_checkpoint++;
		end;
}

poring_rc,45,111,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#1	139,4,4
poring_rc,49,83,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#2	139,4,4
poring_rc,93,66,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#3	139,4,4
poring_rc,119,47,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#4	139,4,4
poring_rc,134,65,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#5	139,4,4
poring_rc,117,107,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#6	139,4,4
poring_rc,95,111,4	duplicate(poring_rc_checkpoint#0)	poring_rc_checkpoint#7	139,4,4



-	script	poring_rc_wall	1085,{
	end;
	
	OnEnable: 
		callsub( L_setup,1 );
		end;
	
	OnInit:
		if ( strnpcinfo(2) == "" ) 
			end;
	OnDisable:
		callsub( L_setup,0 );
		end;
		
	L_setup:
		.@param = getarg( 0,0 );
		.@npc_name$ = strnpcinfo(0);
		
		getmapxy( .@map$,.@x,.@y,1 );
		if ( .@map$ != "" ) {
			if ( .@param ) {
				hideoffnpc .@npc_name$;
			}
			else {
				hideonnpc .@npc_name$;
			}
			setcell .@map$,.@x,.@y,.@x,.@y,cell_walkable,!.@param;
		}
		return;
}

poring_rc,64,114,4	duplicate(poring_rc_wall)	#poring_rc_wall_10	1085
poring_rc,64,113,4	duplicate(poring_rc_wall)	#poring_rc_wall_11	1085
poring_rc,64,112,4	duplicate(poring_rc_wall)	#poring_rc_wall_12	1085
poring_rc,64,111,4	duplicate(poring_rc_wall)	#poring_rc_wall_13	1085
poring_rc,64,110,4	duplicate(poring_rc_wall)	#poring_rc_wall_14	1085
poring_rc,64,109,4	duplicate(poring_rc_wall)	#poring_rc_wall_15	1085
poring_rc,64,108,4	duplicate(poring_rc_wall)	#poring_rc_wall_16	1085

poring_rc,70,114,4	duplicate(poring_rc_wall)	#poring_rc_wall_20	1085
poring_rc,70,113,4	duplicate(poring_rc_wall)	#poring_rc_wall_21	1085
poring_rc,70,112,4	duplicate(poring_rc_wall)	#poring_rc_wall_22	1085
poring_rc,70,111,4	duplicate(poring_rc_wall)	#poring_rc_wall_23	1085
poring_rc,70,110,4	duplicate(poring_rc_wall)	#poring_rc_wall_24	1085
poring_rc,70,109,4	duplicate(poring_rc_wall)	#poring_rc_wall_25	1085
poring_rc,70,108,4	duplicate(poring_rc_wall)	#poring_rc_wall_26	1085





-	script	poring_rc_dummy	-1,{
	end;
	
	OnInit:
		if ( strnpcinfo(2) != "" ) {
			.@npc_name$ = strnpcinfo(0);
			getmapxy( .@map$,.@x,.@y,1 );
			.@sprite = rand( 669,682 );
			setnpcdisplay( .@npc_name$,.@sprite );
			while ( 1 ) {
				emotion rand( e_gasp,e_whisp );
				sleep rand( 3000,10000 );
				if ( rand( 100 ) < 10 )
					showscript F_Rand( 
						"Go ~ Go ~ Go",
						"Vamos ~ Vamos ~ Vamos ~",
						"¡Prisa!",
						"¡¡Venga!!",
						"¡Haz tu mejor esfuerzo!",
						"Más rápido...",
						"Tienes que estar bromeando...",
						"No puedes ser tan lento ...",
						"¡No mires atrás, CORRE!",
						"Pecopeco lo mejor!",
						"Corre ídolo ~ corre ~"
					);
			}
		}
		end;
}

poring_rc,52,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_1	1905
poring_rc,52,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_2	1905
poring_rc,52,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_3	1905
poring_rc,52,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_1_4	1905

poring_rc,57,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_1	1905
poring_rc,57,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_2	1905
poring_rc,57,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_3	1905
poring_rc,57,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_2_4	1905

poring_rc,62,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_1	1905
poring_rc,62,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_2	1905
poring_rc,62,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_3	1905
poring_rc,62,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_3_4	1905

poring_rc,67,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_1	1905
poring_rc,67,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_2	1905
poring_rc,67,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_3	1905
poring_rc,67,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_4_4	1905

poring_rc,72,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_1	1905
poring_rc,72,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_2	1905
poring_rc,72,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_3	1905
poring_rc,72,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_5_4	1905

poring_rc,77,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_1	1905
poring_rc,77,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_2	1905
poring_rc,77,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_3	1905
poring_rc,77,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_6_4	1905

poring_rc,82,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_1	1905
poring_rc,82,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_2	1905
poring_rc,82,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_3	1905
poring_rc,82,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_7_4	1905

poring_rc,87,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_1	1905
poring_rc,87,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_2	1905
poring_rc,87,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_3	1905
poring_rc,87,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_8_4	1905

poring_rc,92,124,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_1	1905
poring_rc,92,122,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_2	1905
poring_rc,92,119,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_3	1905
poring_rc,92,116,4	duplicate(poring_rc_dummy)	#poring_rc_dummy_9_4	1905




-	script	poring_rc_trap	-1,{
	
	OnTouch:
		specialeffect2 EF_SKIDTRAP;
		if ( getvariableofnpc( .status,"poring_rc_main" ) == 2 ) {
			.@sc = F_Rand( 
				SC_STONE,
				SC_FREEZE,
				SC_STUN,
				SC_SLEEP,
				SC_POISON,
				SC_CURSE,
				SC_CONFUSION,
				SC_BLIND,
				SC_DEC_AGI,
				SC_ILLUSION,
				SC_SLOWDOWN
			);
			unitstopwalk getcharid(3);
			sc_start .@sc,rand( 3000,5000 ),1;
		}
		end;
}


// adjust the coordinate of traps here.
// duplicate more if you need more.
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_1	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_2	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_3	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_4	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_5	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_6	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_7	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_8	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_9	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_10	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_11	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_12	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_13	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_14	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_15	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_16	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_17	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_18	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_19	4_SOIL,2,2
poring_rc,1,1,4	duplicate(poring_rc_trap)	???#poring_rc_trap_20	4_SOIL,2,2
